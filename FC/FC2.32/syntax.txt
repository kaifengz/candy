<程序>			::=	{ <函数预声明> | <函数定义> }
<函数预声明>	::=	<函数头> ;
<函数定义>		::=	<函数头> "{" { <语句> } "}"
<函数头>		::=	<类型> <标识符> "(" [ void | <类型> [ "[" "]" ] <标识符> { , <类型> [ "[" "]" ] <标识符> } ")"
<语句>			::=	<变量定义> |
					<输入语句> |
					<输出语句> |
					<选择语句> |
					<开关语句> |
					<复合语句> |
					<转移语句> |
					<赋值语句> |
					<while循环> |
					<函数调用> ; |
					<do-while循环> |
					<void库函数调用> ;
<类型>			::= char | int | double
<标识符>
<变量定义>		::=	<类型> ( <标识符> [ = <表达式> ] | "[" <整数> "]" <标识符> [ = <字符串> ] ) { , <标识符> [ = <表达式> ] | "[" <整数> "]" <标识符> [ = <字符串> ] } ;
<输入语句>		::=	input ( "[" "]" <标识符> | <标识符> [ "[" <表达式> "]" ] ) { , "[" "]" <标识符> | <标识符> [ "[" <表达式> "]" ] } ;
<输出语句>		::=	output ( "[" "]" <标识符> | <表达式> | <字符串> ) { , ( "[" "]" <标识符> | <表达式> | <字符串> ) } ;
<选择语句>		::= if "(" <条件> ")" <语句> [ else <语句> ]
<开关语句>		::= switch "(" <表达式> ")" "{" { case ( <字符> | <整数> ) ":" { <语句> } } [ default ":" { <语句> } ] "}"
<转移语句>		::= ( break | continue | exit | return [ <表达式> ] ) ;
<复合语句>		::= "{" { <语句> } "}"
<赋值语句>		::= <标识符> [ "[" <表达式> "]" ] <赋值运算符> <表达式>;
<while循环>		::= while "(" <条件> ")" <语句>
<函数调用>		::= <标识符> "(" ( "[" "]" <标识符> | <表达式> ) { , ( "[" "]" <标识符> | <表达式> ) } ")"
<do-while循环>	::= do <语句> while "(" <条件> ")" ;
<表达式>		::= [ + | -] <项> { ( + | - ) <项> }
<整数>
<字符串>
<条件>			::= <条件与项> { "||" <条件与项> }
<字符>
<赋值运算符>	::= = | += | -= | *= | /= | %=
<项>			::= <因子> { ( * | / | % ) <因子> }
<条件与项>		::= <条件子项> { "&&" <条件子项> }
<因子>			::= <数> | <标识符> | [ <类型> ] "(" <表达式> ")" | <函数调用> | <返回库函数调用>
<条件子项>		::= ! <因子> | <表达式> [ <比较运算符> <表达式> ]
<比较运算符>	::= == | != | < | <= | > | >=
<数>			::= <字符> | <整数> | <实数>
<实数>
